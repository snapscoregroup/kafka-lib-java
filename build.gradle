plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
    id "com.jfrog.artifactory" version "4.28.2"
}

group 'com.sts'
version "${JAR_VERSION}"

repositories {
    mavenCentral()
    maven {
        url 'https://deploy.snapscore.dev/artifactory/libs-snapshot'
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
    maven {
        url 'https://deploy.snapscore.dev/artifactory/libs-releases'
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
    maven {
        name = "GitHubPackages"
        url 'https://maven.pkg.github.com/snapscoregroup/*'
        credentials {
            username = "${github_user}"
            password = "${github_password}"
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    implementation 'io.projectreactor.kafka:reactor-kafka:1.3.17'
}

shadowJar {
    archiveBaseName = project.name
    classifier = ''
    archiveVersion = version
}

jar {
    manifest {
        attributes 'Implementation-Title': 'PROTOBUF',
                'Implementation-Version': version
    }
}

// Allows the Fat Jar to be uploaded to Artifactory
artifacts {
    archives shadowJar
}
artifactoryPublish {
    dependsOn shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    //The base Artifactory URL if not overridden by the publisher/resolver
    contextUrl = "https://deploy.snapscore.dev/artifactory"
    publish {
        //A closure defining publishing information
        repository {
            // The Artifactory repository key to publish to, ex. 'libs-snapshot'
            repoKey = "${repo}"

            // The publisher user name
            username = "${artifactory_user}"

            // The publisher password
            password = "${artifactory_password}"
        }
        publishArtifacts = true
        publishPom = true
        defaults {
            publications('mavenJava')
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

test {
    useJUnitPlatform()
}
