//file:noinspection GroovyUnusedAssignment
//file:noinspection GrDeprecatedAPIUsage
group 'com.snapscore'
version "${JAR_VERSION}"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.0.0'

        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"

        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
    }
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 20.0
targetCompatibility = 20.0

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    implementation 'io.projectreactor.kafka:reactor-kafka:1.3.21'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName = project.name
    archiveClassifier = ''
    archiveVersion = version
}

jar {
    manifest {
        attributes 'Implementation-Title': 'kafka-lib',
                'Implementation-Version': version
    }
}

// Allows the Fat Jar to be uploaded to Artifactory
artifacts {
    archives shadowJar
    archives jar
}

configurations {
    fatJarRuntimeElements {
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.JAR))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EMBEDDED))
        }
        outgoing.artifact(shadowJar)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/snapscoregroup/kafka-lib-java")
            credentials(HttpHeaderCredentials) {
                name "Authorization"
                value "Bearer ${System.getenv("GITHUB_TOKEN")}"
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

